// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {
    static int delayTime;

    /** Performs all the initializations required by the OS. */
    function void init() {
        let delayTime = 300; // Time delay for 1 millisecond^M
        do Math.init();
        do Memory.init();
        do Screen.init();
        do Main.main();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while (true) {
            // loop forever
        }
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int ms, t;
        let ms = 0;
        while (ms < duration) {
            let t = 0;
            while (t < delayTime) { // loop until delayTime
                let t = t + 1;
            }
            let ms = ms + 1; // Approx 1 millisecond
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printString("ERR");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}
